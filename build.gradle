plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id 'net.researchgate.release' version '2.1.2'
}

apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'idea'

apply from: 'libraries.gradle'
apply from: 'gradle/license.gradle'
apply from: 'gradle/release.gradle'

apply plugin: 'findbugs'
apply plugin: 'pmd'

sourceCompatibility = 1.7

group = "io.jmnarloch"
archivesBaseName="gocd-health-check-plugin"

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Go Continous Delivery Health Check plugin',
                   'Implementation-Version': version
    }
    into('lib') {
        from configurations.compile
    }
}

repositories {
    jcenter()
}

configurations {
    provided
}

idea {
    project {
        languageLevel = sourceCompatibility
    }
    module{
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

dependencies {
    provided (libraries.goPluginApi)

    compile (libraries.gocdTaskPluginApi)
    compile (libraries.rxJava)
    compile (libraries.rxNetty)
    compile (libraries.jackson)

    compile (libraries.gson)
    compile (libraries.commonsIO)
    compile (libraries.commonsLang)

    testCompile (libraries.junit)
    testCompile (libraries.mockito)
}

sourceSets.main.compileClasspath += [configurations.provided]
sourceSets.test.compileClasspath += [configurations.provided]
sourceSets.test.runtimeClasspath += [configurations.provided]

processResources {
    expand(project.properties)
}

javadoc {
    classpath += [configurations.provided]
}

findbugs {
    ignoreFailures = true
}
